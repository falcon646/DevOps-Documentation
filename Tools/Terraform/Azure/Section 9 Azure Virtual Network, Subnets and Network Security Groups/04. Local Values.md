### **Local Values**

**Local values** in Terraform are used to assign expressions to a name, making configurations cleaner and easier to manage. They are defined using the `locals` block and allow users to avoid redundant expressions by reusing computed values across a Terraform configuration.

- **Purpose of Local Values**: Local values allow you to define an expression once and then reuse it multiple times within the module, which helps in avoiding repetition and makes your code cleaner.
    - Think of local values like temporary local variables in a function. Once declared, they can be referenced throughout the configuration as `local.<value_name>`.
    - Instead of repeating complex expressions or values throughout the configuration, you can store them in local values and reference them, making your configuration more maintainable.
   - The key advantage of local values is the ability to easily change the value in one central location. This is especially useful if a value or expression is used in multiple places, as it saves you from making changes in several places.
    - Overusing local values could lead to clutter, so it’s important to balance their use and only define them when necessary.
    - Local Values Improves Readability: Assign meaningful names to expressions, making configurations easier to understand.
    - It avoids Repetition: Instead of repeating the same expression multiple times, define it once and reuse it.
    - It simplifies Complex Expressions: Helps break down long or complex expressions into smaller, manageable components.

**Declaring Local Values**
Local values are declared using the `locals` block:
```json
locals {
  service_name = "forum"
  owner        = "Community Team"
}
//
locals {
  instance_name   = "web-server"
  instance_count  = 3
  environment     = "production"
  tags = {
    Name        = "TerraformInstance"
    Environment = "Production"
  }
}
```
The values defined inside `locals` can be accessed using `local.<name>`.

**Implementing Local Values in Demo**

Local values are ideal for setting values that you want to reference multiple times but don't want to repeat. We will use local values to define tags for resources, such as **owners** and **environment**, which are common to many resources in Azure.
- **Owner Tag**: The owner tag will hold the business division (e.g., `SAP`) from the **business_division** variable.
- **Environment Tag**: The environment tag will hold the environment (e.g., `dev`) from the **environment** variable.
    ```json
    locals {
        owners     = var.business_division
        environment = var.environment
    }
    ```
- **Creating a `common_tags` Map** : Now that we have the **owners** and **environment** values as local values, we can create a **common_tags** map that can be used across all resources. The `common_tags` map will store both **owner** and **environment** tags.
    ```json
    locals {
        owners     = var.business_division
        environment = var.environment
        common_tags = {
            owners     = local.owners
            environment = local.environment
        }
    }
    ```
    - Now, you can use `local.common_tags` wherever tags are allowed in your resources. For example:
    ```json
    resource "azurerm_resource_group" "example" {
        name     = "rg-example"
        location = var.resource_group_location
        tags     = local.common_tags
    }
    ```
    - This way, the **owner** and **environment** tags are automatically added to the resource group, based on the values of the **business_division** and **environment** variables.
- **Creating a Resource Name Prefix** : This is useful for ensuring consistency in naming conventions across all resources. For example, the resource name prefix can be based on the **business division** and **environment**. We will concatenate these two values with a hyphen
    ```json
    locals {
        owners     = var.business_division
        environment = var.environment
        resource_name_prefix = "${var.business_division}-${var.environment}"
        common_tags = {
            owners     = local.owners
            environment = local.environment
        }
    }
    ```
    - This allows you to easily generate a consistent prefix for all resources, such as `sap-dev`, `hr-staging`, etc. You can then use this `local.resource_name_prefix` throughout your Terraform configuration.

**Centralizing Configuration for Easy Updates**

One of the key advantages of using local values is that it allows you to define values centrally. If you need to change the owner or environment for all resources, you only need to update the value in one place — the local values block.

- Imagine you’ve defined this naming convention for **100 resources**. Each resource will automatically follow the `local.resource_name_prefix` pattern. Now, suppose you need to change the prefix across all these resources. The beauty of using local values is that you only need to make the change in one place:
    ```hcl
    locals {
        resource_name_prefix = "${var.business_division}-${var.environment}-${var.resource_group_location}"
    }
    ```
    - With this change, all **100 resources** that use `local.resource_name_prefix` will automatically have their names updated to include the region information. For instance, a resource in the `eastus` region would now have a name like `sap-dev-eastus-vnet` instead of just `sap-dev-vnet`.

---

### **When to Use Local Values vs. Variables**
| Feature            | Input Variables (`variable`) | Local Values (`locals`) |
|--------------------|----------------------------|-------------------------|
| **Purpose**       | External input from users  | Internal reuse within configuration |
| **Scope**         | Module-wide                | Module-wide |
| **Can Change at Runtime?** | Yes (can be overridden) | No (fixed once defined) |
| **Use Case**      | Accept user input          | Store computed values |

